apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 10001
        versionName "1.0.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// get version name
def getVersionName(){
    if(hasProperty('snapshot') && property('snapshot') == true) {
        // property value doesn't matter
        println "********* deploying snapshot **********";
        return android.defaultConfig.versionName + "-SNAPSHOT";
    } else {
        println "********* deploying release **********";
        return android.defaultConfig.versionName;
    }
}

uploadArchives {
    repositories {
        // the maven plugin features this deployer
        mavenDeployer {
            // releases repository
            repository(url: T21_NEXUS_URL + T21_NEXUS_REPO_RELEASE) {
                authentication(userName: T21_NEXUS_USERNAME, password: T21_NEXUS_PASSWORD)
            }
            // snapshot repository - when version name contains '*-SNAPSHOT' this repository is used
            snapshotRepository(url: T21_NEXUS_URL + T21_NEXUS_REPO_SNAPSHOT) {
                authentication(userName: T21_NEXUS_USERNAME, password: T21_NEXUS_PASSWORD)
            }

            // pom project information
            pom.project {
                version getVersionName()
                groupId 'com.tempos21.expandablerecycleradapter'
                artifactId 't21_expandablerecycleradapter_android'
                name 'T21 Android ExpandableRecyclerAdapter Lib'
                packaging 'aar'
            }
        }
    }
}

task install(dependsOn: uploadArchives)

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile "com.android.support:recyclerview-v7:23.1.0"
}
